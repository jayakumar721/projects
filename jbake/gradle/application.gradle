import org.ajoberstar.grgit.Grgit

import java.text.SimpleDateFormat

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.ajoberstar:grgit:1.6.0'
    }
}

apply plugin: 'application'

mainClassName = "org.jbake.launcher.Main"
applicationName = "jbake"

ext {
    examplesBase = "$project.buildDir/examples"

    exampleRepositories = [
            "example_project_freemarker": "git://github.com/jbake-org/jbake-example-project-freemarker.git",
            "example_project_groovy"    : "git://github.com/jbake-org/jbake-example-project-groovy.git",
            "example_project_thymeleaf" : "git://github.com/jbake-org/jbake-example-project-thymeleaf.git",
            "example_project_groovy-mte": "git://github.com/jbake-org/jbake-example-project-groovy-mte.git",
            "example_project_jade"      : "git://github.com/jbake-org/jbake-example-project-jade.git"
    ]
}

processResources {
    from("src/main/resources"){
        include 'default.properties'
        expand  jbakeVersion: project.version,
                timestamp: new SimpleDateFormat("yyyy-MM-dd HH:mm:ssa").format( new Date() )
    }
}



//create clone and Zip Task for each repository
exampleRepositories.each { name, repository ->

    task "clone_${name}Repository"() {
        group = "distribution"
        description "Clone jbake ${name} example project repository"

        def repositoryName = "$examplesBase/$name"

        outputs.dir repositoryName

        doLast {
            Grgit.clone(dir: repositoryName, uri: repository)
        }
    }

    task "${name}Zip"(type: Zip) {
        group "distribution"
        description "Zip $name repository"

        baseName = name
        archiveName = "${baseName}.zip"

        from project.tasks.getByName("clone_${name}Repository").outputs
        exclude 'README.md'
        exclude 'LICENSE'
        exclude '.git'
    }

}

distributions {
    main {
        contents {
            //Include Outputs from zip tasks
            exampleRepositories.each { name, repository ->
                def task = project.tasks.getByName("${name}Zip")
                from(task)
            }
        }
    }
}

startScripts {
    defaultJvmOpts = ['-XX:MaxDirectMemorySize=512m']
    classpath = files("lib","logging")

    /**
     * The startScripts produces scripts that contain `$APP_HOME/lib/lib` in the classpath
     * so we need to tweak it a bit to be able to add the whole lib directory to the classpath :|
     *
     * See https://stackoverflow.com/questions/10518603/adding-classpath-entries-using-gradles-application-plugin
     */
    doLast {
        def windowsScriptFile = file getWindowsScript()
        def unixScriptFile = file getUnixScript()
        windowsScriptFile.text = windowsScriptFile.text.replace('%APP_HOME%\\lib\\lib', '%APP_HOME%\\lib\\*')
        unixScriptFile.text = unixScriptFile.text.replace('$APP_HOME/lib/lib', '$APP_HOME/lib/*')
    }
}

distZip {
    classifier = "bin"
}
